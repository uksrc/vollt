plugins {
    id 'maven-publish'
}

// Describe the library:
version = "2.4-beta"
description = "Java library to set up a TAP service."

// Name of the JAR name (which will be then suffixed by the version number):
archivesBaseName = "tap"

dependencies {
    implementation project(':ADQLLib')
    implementation project(':UWSLib')

    implementation fileTree(dir: '../lib', includes: ['stil_3.3-2.jar'])
    implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'org.json:json:20231013'

    testImplementation 'com.h2database:h2:2.2.224'
    testImplementation fileTree(dir: '../lib', include: 'astroh2-0.4.jar')

    testRuntimeOnly 'simple-jndi:simple-jndi:0.11.4.1'
    testRuntimeOnly 'com.vividsolutions:jts-core:1.14.0'
    testRuntimeOnly 'org.locationtech.spatial4j:spatial4j:0.6'
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.44.1.0'
    
    /* Needed to run Gradle with Java 9+: */
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'

    implementation ("uk.ac.starlink:stil:4.3.1")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'fr.unistra.cds' // Change this to the correct group
            artifactId = 'TAPlib' // Change this for each library
            version = '2.4-SNAPSHOT'

            from components.java
            pom {
                name = 'TAPlib'
                description = 'Java library to deploy a TAP service. This is a cloned version of the original to allow the updating of older java functionality'
                url = 'http://cdsportal.u-strasbg.fr/taptuto/'

                licenses {
                    license {
                        name = 'GNU Lesser General Public License version 3'
                        url = 'https://opensource.org/license/lgpl-3-0'
                    }
                }
                developers {
                    developer {
                        id = 'gmantele'
                        name = 'Gr√©gory Mantelet'
                        email = 'gregory.mantelet@astro.u-strasbg.fr'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/slloyd-src/vollt.git'
                    developerConnection = 'scm:git:ssh://github.com/slloyd-src/vollt.git'
                    url = 'http://example.com/my-library/'
                }
            }
        }
    }

    repositories {
        maven {
            name = "nexus"
            url = uri("https://repo.dev.uksrc.org/repository/maven-snapshots/")
            credentials {
                username = findProperty("nexusUsername") ?: System.getenv("NEXUS_USERNAME")
                password = findProperty("nexusPassword") ?: System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}